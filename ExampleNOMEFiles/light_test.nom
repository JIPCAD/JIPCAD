##   sharp_specs.nom
##   Testfile for sharp CC subdivision
##
##  CHS 2020/12/2
######################################################

bank slider
  set sublevel 3 0 5 1
  set upper_sharpness 9 0 10 0.5
  set lower_sharpness 2 0 10 0.5
  set AMB_RR 0 0 1 0.1
  set AMB_GG 0 0 1 0.1
  set AMB_BB 0 0 1 0.1
  set DIR1_RGB 0 0 1 0.1
  set DIR2_RGB 0 0 1 0.1
  set DIR1_control 0 0 180 1
  set DIR2_control 0 0 180 1
endbank

light amb
  type NOME_AMBIENT
  color ({expr $slider.AMB_RR} {expr $slider.AMB_GG} {expr $slider.AMB_BB})
endlight

light sun1
  type NOME_DIRECTIONAL
  color ({expr $slider.DIR1_RGB} {expr $slider.DIR1_RGB} {expr $slider.DIR1_RGB})
endlight

light sun2
  type NOME_DIRECTIONAL
  color ({expr $slider.DIR2_RGB} {expr $slider.DIR2_RGB} {expr $slider.DIR2_RGB})
endlight



####  Some Surface colors  ###########################
surface M  color  (0.9 0   1  ) endsurface   # Magenta
surface G  color  (0   0.9 0  ) endsurface   # Green
surface W  color  (1   1   1  ) endsurface   # White

point xyz ( 1  1  1) endpoint
point xyc ( 1  1 -1) endpoint
point xbz ( 1 -1  1) endpoint
point xbc ( 1 -1 -1) endpoint
point ayz (-1  1  1) endpoint
point ayc (-1  1 -1) endpoint
point abz (-1 -1  1) endpoint
point abc (-1 -1 -1) endpoint

mesh cube
  face ftop (xyz ayz abz xbz)  surface M  endface   ## We plan to make color specifications on individual faces allowable. ##
  face fbot (xbc abc ayc xyc)  surface W  endface   ## This color info should be maintained in the subdivision process. ##
  face fx   (xyz xbz xbc xyc) endface
  face fy   (xyz xyc ayc ayz) endface
  face fa   (ayc abc abz ayz) endface
  face fb   (abz abc xbc xbz) endface
  sharp {expr $slider.upper_sharpness} (xyz ayz abz xbz xyz) endsharp         ## a very sharp top contour (closed)
  sharp {expr $slider.lower_sharpness} (xbc abc ayc xyc) (xyc xbc) endsharp   ## a softly rounded bootm contour (specified in 2 pieces)
endmesh
###  >>>  The software should check that the specified edges are indeed valid edges in the mesh; if not -> report an error.




subdivision cubeSharp
  sd_type NOME_SD_CC_sharp
  sd_level {expr $slider.sublevel}
  instance icube cube  endinstance
endsubdivision



instance sunn sun1 rotate (0 0 1) ({expr $slider.DIR1_control})   endinstance

instance sunn sun2 rotate (1 0 0) ({expr $slider.DIR2_control})   endinstance


instance ambb amb endinstance



##  >>> maintain color info on faces: The top M patch and the bottom W patch should be showing in the smoothed cube. ##