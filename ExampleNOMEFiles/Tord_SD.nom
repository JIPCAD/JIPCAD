## Tord_SD.nom
##
## Reconstructing the Tord_sculpture from his original 3-2-1 Sculpture at Bridges2020.
## A different way of structuring the facets sinto 6 coherent surface pieces.
## Consolidated with added sharpness specifications.
##
## CHS 2020/12/4

#### Some Surface colors #############################

surface M  color  (0.9 0   1  ) endsurface   # Magenta
surface Z  color  (1   0   0.6) endsurface   # Zinnober
surface R  color  (1   0.1 0  ) endsurface   # Red
surface O  color  (1   0.6 0  ) endsurface   # Orange
surface Y  color  (1   1   0  ) endsurface   # Yellow
surface L  color  (0.5 1   0  ) endsurface   # Lime
surface G  color  (0   0.9 0  ) endsurface   # Green
surface A  color  (0   0.9 0.7) endsurface   # Aqua
surface C  color  (0   1   1  ) endsurface   # Cyan
surface U  color  (0   0.6 1  ) endsurface   # Uniform
surface B  color  (0   0.3 1  ) endsurface   # Blue
surface V  color  (0.3 0   1  ) endsurface   # Violet
surface P  color  (0.6 0   1  ) endsurface   # Purple

surface W  color  (1   1   1  ) endsurface   # White
surface S  color  (0.7 0.7 0.7) endsurface   # Snow
surface D  color  (0.4 0.4 0.4) endsurface   # Dark
surface K  color  (0   0   0  ) endsurface   # blacK

######  coordinate system  #############################

point ooo (0 0 0) endpoint
point xoo (1 0 0) endpoint
point oyo (0 1 0) endpoint
point ooz (0 0 1) endpoint

polyline xax ( ooo xoo ) endpolyline
polyline yax ( ooo oyo ) endpolyline
polyline zax ( ooo ooz ) endpolyline

group coord_axes
    instance ixax xax  surface R  endinstance
    instance iyax yax  surface B  endinstance
    instance izax zax  surface G  endinstance
endgroup

####  Some test B-spline  ##########################
bank p
    set mrd     0.8      0       2     0.01  
    set msl     0.2      0       2     0.01  
    set wdx    -0.5     -1       2     0.1   
    set wdz     1.5      0       3     0.1  
    set wex    -0.4     -1       2     0.1  
    set wey     0.0     -1       2     0.1  
    set wez     1.5      0       3     0.1  
    set wfx    -0.2     -2       2     0.1   
    set wfy    -1.2     -2       2     0.1  
    set wfz     0.8     -2       2     0.1  
    set yrot    0     -180     180     1
    set slicesN 9       0       20     1
endbank

point eaR ( 0.3   6   0 )  endpoint  
point eb  (  0    4   0 )  endpoint  
point ecR ( -0.3  2   0 )  endpoint

point wd  ( {expr  $p.wdx}   1              {expr  $p.wdz} )  endpoint  
point we  ( {expr  $p.wex}  {expr  $p.wey}  {expr  $p.wez} )  endpoint  
point wf  ( {expr  $p.wfx}  {expr  $p.wfy}  {expr  $p.wfz} )  endpoint  

point wg  ( {expr  $p.mrd}   -0.5  {expr  $p.msl} )  endpoint  
point wh  ( {expr  $p.mrd}    0     0 )              endpoint  
point wi  ( {expr  $p.mrd}    0.5  {expr -$p.msl} )  endpoint  


######################################################################################
##  These are the 3 edges connecting the two vertices in Trord Tengstrand's sculpture.
##  Some segments have been selectively de-actovated to reduce clutter when adding faces between the curves.
##  It is only necessary to create 1/6 of the hole surface; the rest is generated by applyiong D3 symmetry.

bspline bs order 4 ( eaR eb ecR  wd we wf wg wh wi )  segs 12  endbspline

instance bsR  bs    surface R                       rotate(0 1 0)({expr $p.yrot}) endinstance
instance bsRu bs    surface O  rotate(1 0 0)(180)   rotate(0 1 0)({expr $p.yrot}) endinstance

instance bsG  bs    surface G                       rotate(0 1 0)({expr $p.yrot+120})  endinstance
instance bsGu bs    surface L  rotate(1 0 0)(180)   rotate(0 1 0)({expr $p.yrot+120})  endinstance

instance bsB  bs    surface B                       rotate(0 1 0)({expr $p.yrot-120})  endinstance   
instance bsBu bs    surface U  rotate(1 0 0)(180)   rotate(0 1 0)({expr $p.yrot-120})  endinstance  


###################################################################################################3
## >>>   The task now is to manually add quads and triangles between adjacent edge curves, and combine them into a mesh.
##       Six copies of that mesh will then make the complete sculpture surface.

## interactively added faces, cleaned up, combined into one mesh:
mesh surf
   face fa ( .bsR.v0 .bsR.v1 .bsG.v1 ) endface 
   face fb ( .bsR.v1 .bsR.v2 .bsG.v2 .bsG.v1 ) endface 
   face fc ( .bsR.v2 .bsR.v3 .bsG.v3 .bsG.v2 ) endface 
   face fd ( .bsR.v3 .bsBu.v9 .bsBu.v8 .bsG.v3 ) endface 
   face fe ( .bsR.v3 .bsR.v4 .bsBu.v10 .bsBu.v9 ) endface 
   face ff ( .bsR.v4 .bsR.v5 .bsBu.v11 .bsBu.v10 ) endface 
   face fg ( .bsR.v5 .bsR.v6 .bsB.v12 .bsBu.v11 ) endface 
   face fh ( .bsR.v6 .bsR.v7 .bsB.v11 .bsBu.v12 ) endface 
   face fi ( .bsR.v7 .bsR.v8 .bsB.v10 .bsB.v11 ) endface 
   face fj ( .bsR.v8 .bsR.v9 .bsB.v9 .bsB.v10 ) endface 
   face fm ( .bsBu.v8 .bsBu.v7 .bsG.v4 .bsG.v3 ) endface 
   face fn ( .bsBu.v7 .bsBu.v6 .bsG.v5 .bsG.v4 ) endface 
  sharp 9 ( .bsR.v0 .bsR.v1 .bsR.v2 .bsR.v3 .bsR.v4 .bsR.v5 .bsR.v6 .bsR.v7 .bsR.v8 .bsR.v9 ) endsharp   ##<<< sEa
  sharp 9 ( .bsG.v0 .bsG.v1 .bsG.v2 .bsG.v3 .bsG.v4 .bsG.v5 ) endsharp   ##<<< sEb
  sharp 9 ( .bsB.v9 .bsB.v10 .bsB.v11 .bsB.v12 ) endsharp   ##<<< sEc
  sharp 9 ( .bsBu.v6 .bsBu.v7 .bsBu.v8 .bsBu.v9 .bsBu.v10 .bsBu.v11 .bsBu.v12) endsharp   ##<<< sEd
endmesh

mesh rimfill
   face fp ( .bsBu.v6 .bsBu.v5 .bsG.v6 .bsG.v5 ) endface 
  sharp 9  ( .bsBu.v6 .bsBu.v5 ) ( .bsG.v6 .bsG.v5 ) endsharp   ##<<<
endmesh

mesh polecaps
   face fq ( .bsBu.v9 .bsGu.v9 .bsRu.v9 ) endface 
   face fr ( .bsG.v9 .bsB.v9 .bsR.v9 ) endface 
endmesh 

## inner polar caps:
instance ipolecaps polecaps  surface W  endinstance

## missing patches in the 3 arms:
instance i0fp rimfill  surface S  endinstance
instance i1fp rimfill  surface S  rotate(0 1 0)(120)  endinstance
instance i2fp rimfill  surface S  rotate(0 1 0)(240)  endinstance


instance surf1 surf  surface Y         endinstance 
instance surf2 surf  surface O  rotate(0 1 0)(120)  endinstance 
instance surf3 surf  surface C  rotate(0 1 0)(240)  endinstance

instance surf4 surf  surface G  rotate(1 0 0)(180)     endinstance
instance surf5 surf  surface R  rotate(1 0 0)(180)   rotate(0 1 0)(120)  endinstance
instance surf6 surf  surface B  rotate(1 0 0)(180)   rotate(0 1 0)(240)  endinstance

###############  END  ######################################################

